/*
 *	RISC-V Instruction Set
 *    RV32I Base Integer Instruction Set
 *
 *	This file is part of GLISS
 *	Copyright (c) 2017, IRIT UPS.
 *
 *	GLISS is free software; you can redistribute it and/or modify
 *	it under the terms of the GNU General Public License as published by
 *	the Free Software Foundation; either version 2 of the License, or
 *	(at your option) any later version.
 *
 *	GLISS is distributed in the hope that it will be useful,
 *	but WITHOUT ANY WARRANTY; without even the implied warranty of
 *	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *	GNU General Public License for more details.
 *
 *	You should have received a copy of the GNU General Public License
 *	along with GLISS; if not, write to the Free Software
 *	Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 */

// Integer and Bitwise Register-Register Operations
op all_rrr =
	  add 
	| and
	| or
	| sll
	| slt
	| sltu
	| sra
	| srl
	| sub
	| xor

// Integer Register-Immediate Operations
op all_rri =
	  addi
	| andi
	| ori
	| slli
	| slti
	| sltiu
	| srai
	| srli
	| xori

// other ALU instructions
op all_alu =
	  lui
	| auipc


// RV32I Integer Register-Immediate Instructions
//// ADDI, LI, MV, NOP
op addi(imm: int(12), src: reg_t, dest: reg_t)
	syntax =
		if dest == 0 && src == 0 && imm == 0
		then "nop" else 
		if src == 0
		then format("li %s, %d", dest, imm) else if imm == 0
		then format("mv %s, %s", dest, src)
		else format("addi %s, %s, %d", dest, src, imm)
		endif endif endif
	image = format("%12b %s 000 %s 0010011", imm, src, dest)
	action = {	
		setR(dest, R[src] + imm);
	}
	
//// SLTI
op slti(imm: int(12), src: reg_t, dest: reg_t)
	syntax = format("slti %s, %s, %d", dest, src, imm)
	image = format("%12b %s 010 %s 0010011", imm, src, dest)
	action = { setR(dest, if R[src] < imm then 1 else 0 endif); }
	
//// SLTIU, SEQZ
op sltiu(imm: int(12), src: reg_t, dest: reg_t)
	syntax =
		if imm == 1
		then format("seqz %s, %s", dest, src)
		else format("sltiu %s, %s, %d", dest, src, imm)
		endif
	image = format("%12b %s 011 %s 0010011", imm, src, dest)	
	action = { setR(dest, if coerce(uword, R[src]) < coerce(uword, imm) then 1 else 0 endif); }

//// ANDI
op andi(imm: int(12), src: reg_t, dest: reg_t)
	syntax = format("andi %s, %s, %d", dest, src, imm)
	image = format("%12b %s 111 %s 0010011", imm, src, dest)
	action = {
		setR(dest, R[src] & imm);
	}
	
//// ORI
op ori(imm: int(12), src: reg_t, dest: reg_t)
	syntax = format("ori %s, %s, %d", dest, src, imm)
	image = format("%12b %s 110 %s 0010011", imm, src, dest)
	action = {	
		setR(dest, R[src] | imm);
	}

//// XORI, NOT
op xori(imm: int(12), src: reg_t, dest: reg_t)
	syntax =
		if imm == -1
		then format("not %s, %s", dest, src)
		else format("xori %s, %s, %d", dest, src, imm)
		endif
	image = format("%12b %s 100 %s 0010011", imm, src, dest)
	action = {	
		setR(dest, R[src] ^ imm);
	}
	
//// LUI	
op lui(immediate: int(20), dest: reg_t)
	syntax = format("lui %s, %d", dest, immediate)
	image  = format("%20b %s 0110111", immediate, dest)
	action = { setR(dest, coerce(word, immediate) << 12); }
	
//// SLLI	
op slli(shamt: card(6), src: reg_t, dest: reg_t)
	syntax = format("slli %s, %s, %d", dest, src, shamt)
	image  = format("000000 %6b %s 001 %s 0010011", shamt, src, dest)
	action = {
		R[dest] = R[src] << shamt;
	}
	
//// SRLI	
op srli(shamt: card(6), src: reg_t, dest: reg_t)
	syntax = format("srli %s, %s, %d", dest, src, shamt)
	image  = format("000000 %6b %s 101 %s 0010011", shamt, src, dest)
	action = {
		R[dest] = coerce(uword, R[src]) >> shamt;
	}

//// SRAI		
op srai(shamt: card(6), src: reg_t, dest: reg_t)
	syntax = format("srai %s, %s, %d", dest, src, shamt)
	image  = format("010000 %6b %s 101 %s 0010011", shamt, src, dest)
	action = {
		R[dest] = coerce(word, R[src]) >> shamt;
	}

//// AUIPC		
op auipc(immediate: int(20), dest: reg_t)
	syntax = format("auipc %s, %d", dest, immediate)
	image  = format("%20b %s 0010111", immediate, dest)
	action = { 
		setR(dest, __IADDR + (coerce(word, immediate) << 12)); 
	}

// RV32I Integer Register-Register Operations

//// ADD	
op add(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("add %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 000 %s 0110011", src2, src1, dest)
	action = {	
		setR(dest, R[src1] + R[src2]);
}

//// SLT, SLTZ, SGTZ
op slt(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = 
		if src2 == 0
		then format("sltz %s, %s", dest, src1) else if src1 == 0
		then format("sgtz %s, %s", dest, src2)
		else format("slt %s, %s, %s", dest, src1, src2)
		endif endif
	image = format("0000000 %s %s 010 %s 0110011", src2, src1, dest)
	action = { setR(dest, if R[src1] < R[src2] then 1 else 0 endif); }

//// SLTU, SNEZ
op sltu(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax =
		if src1 == 0
		then format("snez %s, %s", dest, src2)
		else format("sltu %s, %s, %s", dest, src1, src2)
		endif
	image = format("0000000 %s %s 011 %s 0110011", src2, src1, dest)
	action = { setR(dest, if coerce(uword, R[src1]) < coerce(uword, R[src2]) then 1 else 0 endif); }
	
//// AND	
op and(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("and %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 111 %s 0110011", src2, src1, dest)
	action = {
		setR(dest, R[src1] & R[src2]);
	}
	
//// OR	
op or(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("or %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 110 %s 0110011", src2, src1, dest)
	action = {
		setR(dest, R[src1] | R[src2]);
	}

//// XOR	
op xor(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("xor %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 100 %s 0110011", src2, src1, dest)
	action = {
		setR(dest, R[src1] ^ R[src2]);
	}	
	
//// SLL
op sll(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("sll %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 001 %s 0110011", src2, src1, dest)
	action = {
		let s = src2<4..0>;
		let temp = R[src1] << s;
		R[dest] = temp;
	}
	
//// SRL
op srl(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("srl %s, %s, %s", dest, src1, src2)
	image = format("0000000 %s %s 101 %s 0110011", src2, src1, dest)
	action = {
		let s = src2<4..0>;
		let temp = coerce(uword, R[src1]) >> s;
		R[dest] = temp;
	}

//// SUB, NEG	
op sub(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax =
		if src1 == 0
		then format("neg %s, %s", dest, src2)
		else format("sub %s, %s, %s", dest, src1, src2)
		endif
	image = format("0100000 %s %s 000 %s 0110011", src2, src1, dest)
	action = {
		setR(dest, R[src1] - R[src2]);
	}
	
//// SRA	
op sra(src2: reg_t, src1: reg_t, dest: reg_t)
	syntax = format("sra %s, %s, %s", dest, src1, src2)
	image = format("0100000 %s %s 101 %s 0110011", src2, src1, dest)
	action = {
		let s = src2<4..0>;
		let temp = coerce(word, R[src1]) >> s;
		R[dest] = temp;
	}
